plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.google.cloud.tools.jib' version '3.4.0'
    id 'jacoco'
    id 'com.github.hierynomus.license' version '0.16.1'
}

group = 'com.hubsante'
archivesBaseName = 'hubsante-dispatcher'
version = '0.12-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

springBoot {
    mainClass = 'com.hubsante.hub.HubApplication'
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    /*
    * We use mavenCentral() for every dependency but the models library, which is published on GitHub Packages.
    * So we add a additional maven repository to fetch it
    *
    * During development, it can be convenient to be able to test changes in the models lib without having to publish it
    * In this case, we should execute the gradle task publishToMavenLocal() in the models project and then uncomment
    * the mavenLocal() line below. It will push the models lib to the local .m2 folder, and the gradle build on this project
    * will fetch it from there
     */
//    mavenLocal()
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/ansforge/SAMU-Hub-Modeles")
        credentials {
            username = System.getenv('GITHUB_ACTOR')
            password = System.getenv('GITHUB_TOKEN')
        }
    }
}

ext {
    set('testcontainersVersion', "1.17.6")
}

dependencies {
    implementation 'com.hubsante:models-2:0.4.0'

    implementation 'org.codehaus.groovy:groovy-json:3.0.9'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'com.github.jknack:handlebars:4.3.1'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.json:json:20220924'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.5'
    implementation 'com.networknt:json-schema-validator:1.0.87'
    implementation 'org.projectlombok:lombok'
    implementation 'org.junit.jupiter:junit-jupiter-params'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.1.2'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.1.2'
    implementation 'org.springframework.boot:spring-boot-starter-aop:3.1.2'
    implementation 'org.aspectj:aspectjtools:'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus:1.11.2'

    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:rabbitmq'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

license {
    header = file('LICENSE_HEADER.txt')
    strictCheck = true

    ext {
        company = "Agence du Numerique en Sante (ANS)"
        year = licenseYear()
    }

    mapping 'java', 'JAVADOC_STYLE'
    include "**/*.java"
    exclude "**/test/**"
}

jib {
    from {
        image 'eclipse-temurin:21-jre'
    }
    to {
        image 'romainfd/dispatcher:latest'
        tags = gitRefs()
    }
}

import groovy.json.JsonSlurper

def gitRefs() {
    try {
        def releaseCommitHash = null
        def tagName = null

        if (System.getenv('GITHUB_TOKEN')) {
            def releaseData = callGithubAPI('releases/latest')
            tagName = releaseData.tag_name

            def commitData = callGithubAPI('commits/tags/' + tagName)
            releaseCommitHash = commitData.sha.take(7)
            println("Release commit hash: " + releaseCommitHash)
        }

        new ByteArrayOutputStream().withStream { os ->
            exec {
                commandLine 'git', 'rev-parse', '--short=7', 'HEAD'
                standardOutput = os
            }
            def currentCommitHash = os.toString().trim()
            println("Current commit hash: " + currentCommitHash)

            if (currentCommitHash == releaseCommitHash) {
                return ['latest', currentCommitHash, tagName]
            } else {
                return [currentCommitHash]
            }
        }
    } catch (Exception e) {
        throw new GradleException('Failed to retrieve Git commit hash.', e)
    }
}

static def licenseYear() {
    def currentYear = new Date().format('yyyy')
    def licenseYear

    if (currentYear != "2023") {
        licenseYear = "2023-" + currentYear
    } else {
        licenseYear = "2023"

    }
    return licenseYear
}

static def callGithubAPI(String endpoint) {
    // GitHub API base URL
    def apiUrl = 'https://api.github.com/repos/ansforge/SAMU-Hub-Sante/'
    def token = System.getenv('GITHUB_TOKEN')
    def request = new URL(apiUrl + endpoint).openConnection()
    request.setRequestProperty('Authorization', 'token ' + token)
    request.connect()

    return new JsonSlurper().parseText(request.getInputStream().getText())
}
